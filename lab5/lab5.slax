version 1.1;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns xutil extension = "http://xml.libslax.org/xutil";
ns bit extension = "http://xml.libslax.org/bit";
ns curl extension = "http://xml.libslax.org/curl";

import "../import/junos.xsl";

template junoscript() {
    var $new-password = jcs:get-secret("Enter your new password:  ");
    var $confirm-pass = jcs:get-secret("Confirm the new password: ");
    if (jcs:empty($new-password) || $new-password != $confirm-pass ) {
        <output> "Password is invalid! Password not changed.";
    }
    else {
        <output> "Password is valid.";
        <output>;
        var $configuration = {
            <configuration> {
                <system> {
                    <login> {
                        <user> {
                            <name> $user;
                            <authentication> { 
                                <plain-text-password-value> $new-password;
                            }
                        }
                    }
                }
            }
        }
        var $connection = jcs:open();
        var $results := {
            call jcs:load-configuration($connection, $configuration);
        }
        if ($results//xnm:error) {
            <output> jcs:output("Error changing password");
            for-each ($results//xnm:error) {
                <output> message;
            }
        }
        else {
            <output> "Password changed.";
        }
        var $close-results = jcs:close($connection);
    }
}

